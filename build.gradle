plugins {
    id 'application'
    id 'eclipse'
    id 'com.google.cloud.tools.jib' version '2.7.1'
    id 'org.javamodularity.moduleplugin' version '1.7.0'
}

wrapper.gradleVersion = '6.8.1'
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
modularity.improveEclipseClasspathFile()

repositories {
    jcenter()
    google()
    mavenCentral()
}

dependencies {
    configurations.implementation.canBeResolved = true

    implementation 'org.apache.geronimo.specs:geronimo-json_1.1_spec:1.5'
    implementation 'org.apache.geronimo.specs:geronimo-jsonb_1.0_spec:1.4'
    implementation 'org.apache.johnzon:johnzon-jsonb:1.2.10'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

application {
    mainModule = moduleName
    mainClassName = 'cyou.obliquerays.api.msg.MessagingProcess'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withSourcesJar()
    withJavadocJar()
}

jar {
    version = buildMajor + '.' + buildMinor + '.' + buildRevision
    manifest {
        attributes('Name': 'cyou.obliquerays.api.msg')
        attributes('Specification-Title': project.name)
        attributes('Specification-Version': version)
        attributes('Specification-Vendor': 'cyou.obliquerays')
        attributes('Implementation-Title': project.name)
        attributes('Implementation-Version': version)
        attributes('Implementation-Vendor': 'cyou.obliquerays')
        attributes('Automatic-Module-Name': moduleName)
    }
    doLast {
        file('gradle/release').mkdirs()
        delete {
            delete files('gradle/release')
        }
        copy {
            from configurations.implementation
            into 'gradle/release'
        }
        copy{
            from jar
            into 'gradle/release'
        }
        def currentRevision = buildRevision.toInteger() + 1
        def File propsFile = file('gradle.properties')
        if (propsFile.canWrite()) {
            def Properties props = new Properties()
            props.load(new FileReader(propsFile))
            props['buildRevision'] = currentRevision.toString()
            props.store(propsFile.newWriter(), null)
        }
    }
}

tasks.withType(Javadoc) {
    failOnError = false
    options {
        memberLevel = JavadocMemberLevel.PRIVATE
        encoding = 'UTF-8'
        links("http://docs.oracle.com/javase/jp/11/docs/api/")
    }
}

compileTestJava {
//    onlyIf { project.hasProperty('goTest') }
}

test {
//    onlyIf { project.hasProperty('goTest') }
    moduleOptions {
        runOnClasspath = true
    }
    useJUnitPlatform()
}

jib {
	version = buildMajor + '.' + buildMinor + '.' + buildRevision
    extraDirectories {
        paths = 'gradle/release'
    }
    from {
        image = 'openjdk:11-jre-slim'
    }
    to {
        image = 'msg-transfer:' + version // YOUR_IMAGE_NAME
    }
}
