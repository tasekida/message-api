plugins {
    id 'application'
    id 'com.google.cloud.tools.jib' version '2.7.1'
    id 'org.javamodularity.moduleplugin' version '1.7.0'
}

wrapper.gradleVersion = '6.8'
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
modularity.improveEclipseClasspathFile()

repositories {
    google()
    jcenter()
    mavenCentral()
}

dependencies {
    configurations.implementation.canBeResolved = true

    implementation 'org.apache.johnzon:johnzon-core:1.2.9'
	implementation 'org.apache.johnzon:johnzon-mapper:1.2.9'
    implementation 'org.apache.johnzon:johnzon-jsonb:1.2.9'
    implementation 'org.apache.geronimo.specs:geronimo-json_1.1_spec:1.5'
    implementation 'org.apache.geronimo.specs:geronimo-jsonb_1.0_spec:1.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

application {
    mainModule = moduleName
    mainClassName = 'cyou.obliquerays.api.msg.MessagingProcess'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withSourcesJar()
    withJavadocJar()
}

jar {
    version = buildMajor + '.' + buildMinor + '.' + buildRevision
    manifest {
        attributes('Name': 'cyou.obliquerays.api.msg')
        attributes('Specification-Title': project.name)
        attributes('Specification-Version': version)
        attributes('Specification-Vendor': 'cyou.obliquerays')
        attributes('Implementation-Title': project.name)
        attributes('Implementation-Version': version)
        attributes('Implementation-Vendor': 'cyou.obliquerays')
        attributes('Automatic-Module-Name': moduleName)
    }
    doLast {
        file('gradle/libs').mkdirs()
        delete {
            delete files('gradle/libs')
        }
        copy {
            from configurations.implementation
            into 'gradle/libs'
        }
        copy{
            from jar
            into 'gradle/libs'
        }
        def currentRevision = buildRevision.toInteger() + 1
        def File propsFile = file('gradle.properties')
        if (propsFile.canWrite()) {
            def Properties props = new Properties()
            props.load(new FileReader(propsFile))
            props['buildRevision'] = currentRevision.toString()
            props.store(propsFile.newWriter(), null)
        }
    }
}

tasks.withType(Javadoc) {
    failOnError = false
    options {
        memberLevel = JavadocMemberLevel.PRIVATE
        charSet = 'UTF-8'
        encoding = 'UTF-8'
        docEncoding = 'UTF-8'
        links("https://docs.oracle.com/javase/jp/11/docs/api/")
    }
}

test {
    useJUnitPlatform()
}

jib {
    extraDirectories {
        paths = 'gradle/libs'
    }
    from {
//        image = 'adoptopenjdk/openjdk11:jre-11.0.7_10-alpine'
        image = 'openjdk/openjdk:11-jre-slim'
    }
    to {
        image = 'taseki01/msg-transfer' // YOUR_IMAGE_NAME
    }
}
